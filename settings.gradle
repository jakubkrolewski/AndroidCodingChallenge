pluginManagement {
    repositories {
        google {
            content {
                includeGroupByRegex("com\\.android.*")
                includeGroupByRegex("com\\.google.*")
                includeGroupByRegex("androidx.*")
            }
        }
        mavenCentral()
        gradlePluginPortal()
    }
}
dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        google()
        mavenCentral()
    }
}

rootProject.name = "AndroidOTChallenge"
includeAllGradleProjectsInCurrentDirectoryTree()

/* Utility methods */

private List<String> includeAllGradleProjectsInCurrentDirectoryTree() {
    getAllBuildGradleFilesInCurrectDirectoryTree()
            .collect { relativePath(it.parent).replace(File.separator, ':') }
            .each { include(it) }
}

private ConfigurableFileTree getAllBuildGradleFilesInCurrectDirectoryTree() {
    fileTree('.') {
        include '**/build.gradle'
        exclude 'build.gradle' // Exclude the root build file.
    }
}
